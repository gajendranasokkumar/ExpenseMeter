// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
  repositories {
    google()
    mavenCentral()
  }
  dependencies {
    classpath('com.android.tools.build:gradle')
    classpath('com.facebook.react:react-native-gradle-plugin')
    classpath('org.jetbrains.kotlin:kotlin-gradle-plugin')
  }
}

def reactNativeAndroidDir = new File(
  providers.exec {
    workingDir(rootDir)
    commandLine("node", "--print", "require.resolve('react-native/package.json')")
  }.standardOutput.asText.get().trim(),
  "../android"
)

allprojects {
  repositories {
    maven {
      // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
      url(reactNativeAndroidDir)
    }

    google()
    mavenCentral()
    maven { url 'https://www.jitpack.io' }
  }
}

// Exclude legacy support libraries from react-native-push-notification to avoid
// duplicate classes with AndroidX (androidx.core:core)
subprojects { project ->
  afterEvaluate {
    if (project.name.contains('react-native-push-notification')) {
      project.configurations.all { config ->
        config.exclude group: 'com.android.support', module: 'support-compat'
        config.exclude group: 'com.android.support', module: 'support-core-ui'
        config.exclude group: 'com.android.support', module: 'support-core-utils'
        config.exclude group: 'com.android.support', module: 'support-media-compat'
        config.exclude group: 'com.android.support', module: 'support-v4'
      }
    }
  }
}

apply plugin: "expo-root-project"
apply plugin: "com.facebook.react.rootproject"
